apply plugin: 'com.android.application'
apply from: "$rootDir/release.gradle"

// Create a variable called keystorePropertiesFile, and initialize it to
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "alektas.pocketbasket"
        minSdkVersion 21
        targetSdkVersion 28

        versionName "0.9.0"
        versionCode 11
        project.ext.set("archivesBaseName", "PocketBasket-" + defaultConfig.versionName)

        resConfigs "en", "ru"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            buildConfigField 'String', 'AUTHORITY', '"alektas.pocketbasket.ItemsProvider"'
            resValue 'string', 'search_authority', 'alektas.pocketbasket.ItemsProvider'
        }
        debug {
            aaptOptions.cruncherEnabled = false
            minifyEnabled false
            shrinkResources false
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            splits.abi.enable = false
            splits.density.enable = false

            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField 'String', 'AUTHORITY', '"alektas.pocketbasket.debug.ItemsProvider"'
            resValue 'string', 'search_authority', 'alektas.pocketbasket.debug.ItemsProvider'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }
}

// Add plugin https://bitbucket.org/hvisser/android-apt
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha03'
    implementation 'androidx.room:room-runtime:2.2.0-alpha02'
    annotationProcessor 'androidx.room:room-compiler:2.2.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.leanback:leanback:1.1.0-alpha02'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha09'

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.0.1'
    implementation 'com.google.firebase:firebase-ads:18.1.1'
    implementation 'com.google.firebase:firebase-inappmessaging-display:18.0.2'
    implementation 'com.google.firebase:firebase-messaging:19.0.1'

    implementation 'com.google.dagger:dagger:2.24'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.24'

    // Testing
    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
}

apply plugin: 'com.google.gms.google-services'
