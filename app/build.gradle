apply plugin: 'com.android.application'
apply from: "$rootDir/release.gradle"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "alektas.pocketbasket"
        minSdkVersion 21
        targetSdkVersion 29

        versionName "0.10.1"
        versionCode 13
        project.ext.set("archivesBaseName", "PocketBasket-" + defaultConfig.versionName)

        resConfigs "en", "ru"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            buildConfigField 'String', 'AUTHORITY', '"alektas.pocketbasket.ItemsProvider"'
            resValue 'string', 'search_authority', 'alektas.pocketbasket.ItemsProvider'
        }
        debug {
            aaptOptions.cruncherEnabled = false
            minifyEnabled false
            shrinkResources false
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            splits.abi.enable = false
            splits.density.enable = false

            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField 'String', 'AUTHORITY', '"alektas.pocketbasket.debug.ItemsProvider"'
            resValue 'string', 'search_authority', 'alektas.pocketbasket.debug.ItemsProvider'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0-alpha02'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha04'

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'com.google.firebase:firebase-ads:18.3.0'
    implementation 'com.google.firebase:firebase-inappmessaging-display:19.0.3'

    implementation 'com.google.dagger:dagger:2.26'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.26'

    implementation "io.reactivex.rxjava2:rxjava:2.2.19"

    // Testing
    testImplementation 'org.junit.platform:junit-platform-launcher:1.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
    testImplementation "org.mockito:mockito-core:3.2.4"
    androidTestImplementation "android.arch.core:core-testing:1.0.0-alpha5"
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha03'
}

apply plugin: 'com.google.gms.google-services'
